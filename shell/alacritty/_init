#!/usr/bin/env zsh
source ${0:A:h}/../../deploy

install() {
  case $(_os) in
  arch)
    yay --needed --noconfirm -S alacritty yq
    ;;
  debian)
    # For Ubuntu 18 and over
    # sudo add-apt-repository ppa:mmstick76/alacritty
    # sudo apt-get update
    # sudo apt install alacritty

    # Install the Rust compiler with rustup
    ${0:A:h}/../../deploy dev/rust
    rustup override set stable
    rustup update stable

    tempdir=/tmp/temp-install
    (
      ALACRITTY_VERSION=0.5.0
      mkdir -p $tempdir
      cd $tempdir
      $APT_INSTALL cmake pkg-config libfreetype6-dev libfontconfig1-dev libxcb-xfixes0-dev python3
      wget https://github.com/alacritty/alacritty/archive/v${ALACRITTY_VERSION}.tar.gz -O alacritty.tar.gz
      tar xvf alacritty.tar.gz
      cd alacritty-*
      cargo build --release
      sudo cp -f target/release/alacritty /usr/local/bin
      rm -rf $tempdir/alacritty-*
    )
    brew install yq
    ;;
  macos)
    brew install --cask alacritty
    brew install yq
    ;;
  esac
}

# update() {}

link() {
  rm -rf $XDG_CONFIG_HOME/alacritty
  mkdir -p $XDG_CONFIG_HOME/alacritty
  config_file=$XDG_CONFIG_HOME/alacritty/alacritty.yml
  theme_file=one-light-theme.yml
  private_file=private.yml
  (
    cd $DOTFILES/shell/alacritty/config/alacritty
    local config_files=(basic.yml keybinding.yml ${theme_file})
    local yq_expression='select(fi == 0) * select(fi == 1) * select(fi == 2)'
    if [[ -f ${private_file} ]]; then
      config_files+=(${private_file})
      yq_expression+=" * select(fi == 3)"
    fi
    yq eval-all $yq_expression ${config_files[@]} >$config_file
  )
  case $(_os) in
  macos)
    # Increase macos font size by 3
    local increased_font_size=$(($(yq eval '.font.size' $config_file) + 3))
    yq eval ".font.size = $increased_font_size" $config_file --inplace
    ;;
  esac
}

clean() {
  rm -rvf $XDG_CONFIG_HOME/alacritty
}

init "$@"
